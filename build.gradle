import com.architectcoders.wanago.buildsrc.Libs

// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply plugin: "com.github.ben-manes.versions"

buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.AndroidX.Navigation.gradlePlugin
        classpath Libs.gradleVersionsPlugin
        classpath Libs.Hilt.gradlePlugin
    }
    configurations {
        detekt
    }
}

def detektTask = tasks.register("detekt", JavaExec) {
    main = "io.gitlab.arturbosch.detekt.cli.Main"
    classpath = configurations.detekt

    def input = "$projectDir"
    def config = "$projectDir/config/detekt/detekt.yml"
    def exclude = "$projectDir/**/build/**,**/resources/**"
    def params = ['-i', input, '-c', config, '-ex', exclude]

    args(params)
}

dependencies {
    detekt 'io.gitlab.arturbosch.detekt:detekt-cli:1.22.0'
}

// Taken from https://github.com/ben-manes/gradle-versions-plugin
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}