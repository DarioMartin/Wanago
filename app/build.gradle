import com.architectcoders.wanago.buildsrc.Libs

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
}

android {
    namespace 'com.architectcoders.wanago'
    compileSdk 33

    defaultConfig {
        applicationId "com.architectcoders.wanago"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        buildConfigField "String", "ticketMasterApiKey", "\"${properties.getProperty('ticketMasterApiKey')}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation project(":data")
    implementation project(":domain")
    implementation project(":usecases")

    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.appCompat
    implementation Libs.AndroidX.material
    implementation Libs.AndroidX.constraintLayout
    implementation Libs.AndroidX.recyclerView
    implementation Libs.AndroidX.swipeRefresh

    implementation Libs.AndroidX.Fragment.ktx
    implementation Libs.AndroidX.Activity.ktx

    implementation Libs.AndroidX.Navigation.fragmentKtx
    implementation Libs.AndroidX.Navigation.uiKtx

    implementation Libs.AndroidX.Room.ktx
    implementation Libs.AndroidX.Room.library
    kapt Libs.AndroidX.Room.compiler

    implementation Libs.PlayServicesLocation.library

    implementation Libs.OkHttp3.loginInterceptor
    implementation Libs.Retrofit.library
    implementation Libs.Retrofit.converterGson

    implementation Libs.Glide.library
    kapt Libs.Glide.compiler

    implementation Libs.Arrow.library

    implementation Libs.Hilt.android
    kapt Libs.Hilt.compiler

    testImplementation Libs.JUnit.library
    androidTestImplementation Libs.AndroidX.Test.Ext.junit
    androidTestImplementation Libs.AndroidX.Test.Espresso.core
}
